dnl Project: ipv6calc
dnl File   : configure.in
dnl Version: $Id: configure.in,v 1.16 2006/10/28 09:29:30 peter Exp $

dnl Process this file with autoconf to produce a configure script.
AC_INIT(ipv6calc, 0.62.0cvs, ipv6calc@deepspace6.net, ipv6calc)

dnl Define versions of built-in IP location database APIs
VERSION_IP2LOCATION=2.1.0
VERSION_GEOIP=1.4.0

AC_DEFINE(VERSION_IP2LOCATION, "2.1.0", Version of IP2Location API.)
AC_DEFINE(VERSION_GEOIP, "1.4.0", Version of GeoIP API.)

dnl Define version header
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
dnl AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_C_BIGENDIAN

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(strspn strstr)

dnl Check for getopt
AC_CHECK_FUNC(getopt_long, [],
  [
    AC_LIBOBJ(../getopt/getopt)
    AC_LIBOBJ(../getopt/getopt1)
    GETOPT_INCLUDE=-I../getopt
    ENABLE_BUNDLED_GETOPT=1
  ])
AC_SUBST(GETOPT_INCLUDE)
AC_SUBST(ENABLE_BUNDLED_GETOPT)

dnl IP2Location support
AC_ARG_ENABLE([ip2location], 
	AS_HELP_STRING([--enable-ip2location],
	               [Enable IP2Location support (default: disabled)]),
	[if test "x$enable_ip2location" = "xyes"; then
		ENABLE_SUPPORT_IP2LOCATION=1
		AC_DEFINE(SUPPORT_IP2LOCATION, 1, Define if you want IP2Location support.)
	fi])
AC_SUBST(ENABLE_SUPPORT_IP2LOCATION)
AC_SUBST(VERSION_IP2LOCATION)

dnl GeoIP support
AC_ARG_ENABLE([geoip], 
	AS_HELP_STRING([--enable-geoip],
	               [Enable GeoIP support (default: disabled)]),
	[if test "x$enable_geoip" = "xyes"; then
		ENABLE_SUPPORT_GEOIP=1
		AC_DEFINE(SUPPORT_GEOIP, 1, Define if you want GeoIP support.)
	fi])

AC_ARG_ENABLE([geoip-system],
       AS_HELP_STRING([--enable-geoip-system],
               [Use the system GeoIP library instead of the bundled one (default: bundled)]),
       [if test "x$enable_geoip_system" = "xyes"; then
               AC_CHECK_HEADER([GeoIP.h], [], AC_MSG_ERROR([System GeoIP library not found]))
               ENABLE_SYSTEM_GEOIP=1
               ENABLE_SUPPORT_GEOIP=1
               AC_DEFINE(SUPPORT_GEOIP, 1, Define if you want to use GeoIP support.)
               AC_DEFINE(ENABLE_GEOIP_SYSTEM, 1, Define if you want to use GeoIP system library support.)
       fi])

AC_ARG_ENABLE([geoip-default-file],
       AS_HELP_STRING([--enable-geoip-default-file=file],
               [Use a default GeoIP database file when ipv6calc is invoked with '--db-geoip-default|-G']),
       [if test "x$enable_geoip_default_file" != xno; then
               AC_DEFINE_UNQUOTED(GEOIP_DEFAULT_FILE, "$enable_geoip_default_file", Define for a default GeoIP database file.)
               ENABLE_SUPPORT_GEOIP=1
               AC_DEFINE(SUPPORT_GEOIP, 1, Define if you want to use GeoIP support.)
       fi])

AC_SUBST(ENABLE_SUPPORT_GEOIP)
AC_SUBST(VERSION_GEOIP)
AC_SUBST(ENABLE_SYSTEM_GEOIP)

dnl Files to create
AC_OUTPUT([Makefile ipv6calc/Makefile man/Makefile ipv6logconv/Makefile ipv6logstats/Makefile contrib/ipv6calc.spec ipv6calcweb/ipv6calcweb.cgi ])
